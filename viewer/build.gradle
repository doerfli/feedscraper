buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
        classpath("com.moowork.gradle:gradle-node-plugin:${gradleNodePluginVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-jpa'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'
apply plugin: 'jacoco'

group = "li.doerf.feeder"
version = "0.9.0-SNAPSHOT"

dependencies {
    implementation project(":common")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:${kotlinCoroutinesVersion}")

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-websocket")
    implementation("org.springframework.integration:spring-integration-mqtt")

    implementation("io.jsonwebtoken:jjwt-api:${jjwtVersion}")
    implementation("org.flywaydb:flyway-core:${flywayVersion}")
    implementation("com.github.kittinunf.fuel:fuel:${fuelVersion}")
    implementation("com.github.kittinunf.fuel:fuel-coroutines:${fuelVersion}")
    implementation("org.thymeleaf:thymeleaf-spring5:${thymeleafVersion}")

    runtimeOnly("org.springframework.boot:spring-boot-devtools")
    runtimeOnly("org.postgresql:postgresql:${postgresqlVersion}")
    runtimeOnly("io.jsonwebtoken:jjwt-impl:${jjwtVersion}")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:${jjwtVersion}")

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "junit", module: "junit"
    }
    testImplementation("org.springframework.security:spring-security-test")
    testImplementation("org.junit.jupiter:junit-jupiter:${junitVersion}")
    testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")

    testRuntime("com.h2database:h2")
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = 12
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = 12
    }
}

bootJar {
    mainClassName = 'li.doerf.feeder.viewer.ViewerApplication'
}

sourceSets.create("webapp") {
    resources.srcDir("src/webapp/resources")
    resources.srcDir("src/webapp/generated")
}

sourceSets {
    main {
        compileClasspath += sourceSets["webapp"].output
        runtimeClasspath += sourceSets["webapp"].output
    }
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

noArg {
    annotation("li.doerf.feeder.common.util.NoArg")
}

node {
    version = "${nodeVersion}"
    yarnVersion = "${yarnVersion}"
    download = true
    workDir = file("${project.buildDir}/nodejs")
    yarnWorkDir = file('src/webapp/main')
    nodeModulesDir = file('src/webapp/main')
}

task yarnInstall(type: YarnTask) {
    args = ["install"]
}

task compileVue(type: YarnTask) {
    args = ["build"]
}

compileVue.dependsOn yarnInstall
jar.dependsOn compileVue

clean.doFirst {
    delete "${rootDir}/src/webapp/generated/"
    delete "${rootDir}/src/webapp/main/node_modules/"
    delete "${rootDir}/src/webapp/main/yarn-latest/"
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}